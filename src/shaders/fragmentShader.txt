#version 400 core

in vec2 pass_textureCoordinates;
in vec3 surfaceNormal;
in vec3 toLightVector[4];  //vector pointing to the light source
in vec3 toCameraVector; //vector pointing to the camera
in float visibility;    // Simulate fog or reduced visibility

out vec4 out_color;

uniform sampler2D modelTexture;
uniform vec3 lightColor[4];
uniform vec3 attenuation[4];
uniform float shineDamper;          // How much should reflected light scatter from the surface
uniform float reflectivity;         // How reflective is the surface
uniform vec3 skyColor;              // Color of sky for fog / underwater effects.

void main(void) {

    vec3 unitNormal = normalize(surfaceNormal);             //Normal of surface
    vec3 unitVectorToCamera = normalize(toCameraVector);    //Normalize the "to camera" vector to make sure size is 1
    vec3 totalDiffuse = vec3(0.0);
    vec3 totalSpecular = vec3(0.0);

    for (int i = 0; i < 4; i++) {
        float distance = length(toLightVector[i]);
        float atnFactor = attenuation[i].x + (attenuation[i].y * distance) + (attenuation[i].z * distance * distance);
        vec3 unitLightNormal = normalize(toLightVector[i]);                     // Vector pointing TOWARD the light source

        float nDot1 = dot(unitNormal, unitLightNormal);
        float brightness = max(nDot1, 0.0);

        vec3 lightDirection = -unitLightNormal;                                 //Vector pointing AWAY from the light source
        vec3 reflectedLightDirection = reflect(lightDirection, unitNormal);     // use built in reflect func to computer reflected light vector

        float specularFactor = dot(reflectedLightDirection, unitVectorToCamera);// Compute how much reflected light reaches camera
        specularFactor = max(specularFactor, 0.0);                              // Can't have a negative brightneess
        float dampedFactor = pow(specularFactor, shineDamper);                  // make lower spec values even lower without affecting upper values too much.

        totalDiffuse = totalDiffuse + ((brightness * lightColor[i]) / atnFactor);
        totalSpecular = totalSpecular + ((dampedFactor * reflectivity * lightColor[i]) / atnFactor);       // Set the specular light to the color of the light source.
    }

    totalDiffuse = max(totalDiffuse, 0.2);                                     // ensure that we never go fully black (no light)

    // Test the alpha value of the texture's color to see if it is transparent - if it is,  just discard
    vec4 textureColor = texture(modelTexture, pass_textureCoordinates);
    if (textureColor.a < 0.5) {
        discard;
    }

    out_color = vec4(totalDiffuse, 1.0) * textureColor + vec4(totalSpecular, 1.0);
    out_color = mix(vec4(skyColor, 1.0), out_color, visibility);
}